% !TEX root    = kerCoKerDiagram_mp.tex
% !TEX program = metafun

\usemodule[commDiag]

\startMPpage
%showtoken upto ;
%showtoken setupCDgrid ;

setupCDgrid(6, 2cm, 3, 2cm, 0)

% place the CD nodes 

% top row
draw node(p[3], 0, "\node{$0$}");
draw node(p[3], 1, "\node{Ker $f$}");
draw node(p[3], 2, "\node{Ker $a$}");
draw node(p[3], 3, "\node{Ker $b$}");
draw node(p[3], 4, "\node{Ker $c$}");

% top middle row
draw node(p[2], 2, "\node{$A$}");
draw node(p[2], 3, "\node{$B$}");
draw node(p[2], 4, "\node{$C$}");
draw node(p[2], 5, "\node{$0$}");

% bottom middle row
draw node(p[1], 1, "\node{$0$}");
draw node(p[1], 2, "\node{$A'$}");
draw node(p[1], 3, "\node{$B'$}");
draw node(p[1], 4, "\node{$C'$}");

% bottom row
draw node(p[0], 2, "\node{Coker $a$}");
draw node(p[0], 3, "\node{Coker $b$}");
draw node(p[0], 4, "\node{Coker $c$}");
draw node(p[0], 5, "\node{Coker $g'$}");
draw node(p[0], 6, "\node{$0$}");

% add the arrows

% create (but don't draw) the top -> bottom arrow...

save overArrow; path overArrow ;
overArrow :=
  point 4 of p[3] shifted (0.75cm, 0) --
  point 4 of p[3] shifted (1cm, 0){dir 0} ..
  tension 10 ..
  {dir 0}point 2 of p[0] shifted (-1cm, 0) --
  point 2 of p[0] shifted (-0.75cm, 0);


drawoptions (
  withpen pencircle scaled .5pt
  withcolor red
) ;
  
% top row
drawarrow fromtopaths(0, p[3], 0, p[3], 1) ;
drawarrow fromtopaths(0, p[3], 1, p[3], 2) ;
drawarrow fromtopaths(0, p[3], 2, p[3], 3) ;
drawarrow fromtopaths(0, p[3], 3, p[3], 4) ;

drawoptions (
  withpen pencircle scaled .5pt
  withcolor black
) ;

% t -> t-m rows
drawarrow fromtopaths(0, p[3], 2, p[2], 2) ;
drawarrow fromtopaths(0, p[3], 3, p[2], 3) ;
drawarrow fromtopaths(0, p[3], 4, p[2], 4) crossingunder overArrow ;

drawoptions (
  withpen pencircle scaled .5pt
  withcolor blue
) ;

% top middle row
drawarrow fromtopaths.top(0, p[2], 2, p[2], 3, "\nodeSmall{$f$}") ;
drawarrow fromtopaths.ulft(0, p[2], 3, p[2], 4, "\nodeSmall{$g$}") crossingunder overArrow ;
drawarrow fromtopaths(0, p[2], 4, p[2], 5) ;

% t-m -> b-m rows
drawarrow fromtopaths.rt(0, p[2], 2, p[1], 2, "\nodeSmall{$a$}") ;
drawarrow fromtopaths.lrt(0, p[2], 3, p[1], 3, "\nodeSmall{$b$}") crossingunder overArrow ;
drawarrow fromtopaths.rt(0, p[2], 4, p[1], 4, "\nodeSmall{$c$}") ;

% bottom middle row
drawarrow fromtopaths(0, p[1], 1, p[1], 2) ;
drawarrow fromtopaths.lrt(0, p[1], 2, p[1], 3, "\nodeSmall{$f'$}") crossingunder overArrow ;
drawarrow fromtopaths.bot(0, p[1], 3, p[1], 4, "\nodeSmall{$g'$}") ;

drawoptions (
  withpen pencircle scaled .5pt
  withcolor black
) ;

% b-m -> b rows
drawarrow fromtopaths(0, p[1], 2, p[0], 2) crossingunder overArrow ;
drawarrow fromtopaths(0, p[1], 3, p[0], 3) ;
drawarrow fromtopaths(0, p[1], 4, p[0], 4) ;

% bottom row

drawoptions (
  withpen pencircle scaled .5pt
  withcolor red
) ;

drawarrow fromtopaths(0, p[0], 2, p[0], 3);
drawarrow fromtopaths(0, p[0], 3, p[0], 4) ;
drawarrow fromtopaths(0, p[0], 4, p[0], 5) ;
drawarrow fromtopaths(0, p[0], 5, p[0], 6) ;

% top -> bottom arrow...

drawarrow overArrow ;
label.ulft("\nodeSmall{$d$}", point 0.5*length(overArrow) of overArrow) ;

drawoptions (
  withpen pencircle scaled .5pt
  withcolor black
) ;

\stopMPpage
